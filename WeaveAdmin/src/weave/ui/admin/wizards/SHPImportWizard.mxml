<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<admin:WizardViewStack
			xmlns:mx="http://www.adobe.com/2006/mxml"
			xmlns:ui="weave.ui.*"
			xmlns:admin="weave.ui.admin.*"
			xmlns="weave.ui.admin.wizards.*" 
			label="Import shape data"
			creationComplete="handleCreationComplete()"
			>
	<mx:VBox width="100%" height="100%">
		<ui:Indent width="100%" label="Load Shape files" group="{indentGroup}">
			<mx:TextInput id="shpFileNameInput" enabled="true"/>
			<mx:Button label="Upload ..." click="browseAndUploadShapeFiles();"/>
			<mx:Button label="Choose File on Server" click="openServerBrowser();" />
			<mx:Label text="{fileUploadCount > 0 ? fileUploadCount + ' of ' + targetFileUploadCount + ' files uploaded' : ''}"/>
		</ui:Indent>
		<ui:Indent width="100%" group="{indentGroup}">
			<mx:CheckBox id="dbfImportCheckBox" label="Import DBF file as Data Table" selected="true" change="handleKeyColumnsChange()" />
		</ui:Indent>
		<ui:Indent id="dbfColumnNames" enabled="false" label="DBF Key column names" group="{indentGroup}">
			<ui:CustomComboBox id="dbfKeyColumnsCombo" dataProvider="{AdminInterface.instance.dbfKeyColumns}" change="handleKeyColumnsChange()" editable="true"/>
			<ui:HelpComponent>
				Separate DBF column names by commas.  Values in the given columns will be concatenated to form unique identifiers for the shapes. These keys will also be used as the keys of the DataTable entry created from the DBF file.
			</ui:HelpComponent>
		</ui:Indent>
		<admin:SQLTableSelectorDialog indentGroup="{indentGroup}" id="tableSelector" enabled="false" showTableCombo="false" creationComplete="tableSelector.tableControls.label = 'SQL Table Prefix';" activeTable="{shpFileNameInput.text}" tableOverwriteCheckVisible="{AdminInterface.instance.currentUserIsSuperuser}"/>
	</mx:VBox>
	<mx:VBox width="100%" height="100%">
		<ui:IndentTextInput label="Null flag values" enabled="false" id="nullFlagInput" group="{indentGroup}"
				toolTip="Specify flag values to treat as missing data, separated by commas"/>
		<mx:HBox id="geoCol" enabled="false">
			<mx:TextArea width="{indentGroup.measuredIndent}" borderThickness="0" editable="false" selectable="false" wordWrap="true" text="GeometryCollection &amp; DataTable name" textAlign="right"/>
			<mx:HBox verticalAlign="middle">
				<mx:TextInput id="geometryCollectionNameInput" text="{tableSelector.activeTable}"/>
				<mx:CheckBox label="Overwrite" id="configOverwriteCheck" selected="false"/>
				<ui:HelpComponent>
					Both a GeometryCollection and DataTable entry will be created in sqlconfig.xml having the name specified here.
				</ui:HelpComponent>
			</mx:HBox>
		</mx:HBox>
		<ui:Indent id="keyType" enabled="false" label="Key type" group="{indentGroup}">
			<ui:CustomComboBox id="shpKeyTypeCombo" minWidth="100" text="{geometryCollectionNameInput.text}" editable="true" dataProvider="{AdminInterface.instance.keyTypes}" toolTip="{Strings.tip_shp_key_type_dropdown}"/>
			<mx:Button label="Refresh" click="AdminInterface.instance.getKeyTypes()"/>
			<ui:HelpComponent>
				If two tables have compatible keys, you should give them the same key type. <!--
				-->If two tables have incompatible keys, they should not have the same key type. <!--
				-->Weave only allows two columns to be compared if they have the same key type.
			</ui:HelpComponent>
		</ui:Indent>
		<ui:Indent id="projectionCode" enabled="false" label="Projection SRS" group="{indentGroup}">
			<mx:TextInput id="srsProjectionTxtInput" text="EPSG:4326"/>  <!-- is it a good idea to use the standard lat/long as the default? -->
			<mx:Button id="srsProjectionValidateBtn" label="Validate" click="validateProjectionSRS()"/>
		</ui:Indent>
	</mx:VBox>
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.utils.StringUtil;
			
			import weave.Strings;
			import weave.api.WeaveAPI;
			import weave.services.AdminInterface;
			import weave.services.DelayedAsyncInvocation;
			import weave.services.WeaveAdminService;
			import weave.ui.IndentGroup;
			import weave.ui.admin.ServerFileViewer;
			
			private static const paddingIndent:int = 20;
			private const indentGroup:IndentGroup = new IndentGroup();
			
			private function handleCreationComplete():void
			{
				shapeFileReferenceList.addEventListener(Event.SELECT, shapeFileSelectedHandler);
				
			
				
				backEnabled = false;
				nextEnabled = false;
				finishEnabled = false;
			}
			
			[Bindable] private var fileUploadCount:int = 0;
			[Bindable] private var targetFileUploadCount:int = 0;
			private var _dbfFileToRequestColumnNamesFrom:FileReference;
			private function shapeFileSelectedHandler(event:Event):void
			{
				fileUploadCount = 0;
				targetFileUploadCount = shapeFileReferenceList.fileList.length;
				shpFileNameInput.text = '';
				for each (var fileRef:FileReference in shapeFileReferenceList.fileList)
				{
					var name:String = fileRef.name;
					var ext:String = name.substr(name.length - 4).toLowerCase(); // lower case extension
					var nameWithoutExt:String = name.substr(0, name.length - 4);
					if (ext == ".shp")
					{
						if (shpFileNameInput.text != '')
							shpFileNameInput.text += ',';
						shpFileNameInput.text += nameWithoutExt;
					}
					if (ext == ".dbf")
					{
						AdminInterface.instance.dbfKeyColumns = [];
						_dbfFileToRequestColumnNamesFrom = fileRef;
					}
					
					fileRef.addEventListener(IOErrorEvent.IO_ERROR, fileErrorHandler);
					fileRef.addEventListener(Event.COMPLETE, handleFileLoadComplete);
					fileRef.load();
				}
			}
			
			private var shapeFileReferenceList:FileReferenceList = new FileReferenceList();
			private function browseAndUploadShapeFiles():void
			{
				var fileTypes:Array = [new FileFilter("Shape Files", "*.*")];
				shapeFileReferenceList.browse(fileTypes);
			}

			private function fileErrorHandler(event:IOErrorEvent):void
			{
				var fileRef:FileReference = event.target as FileReference;
				if (shapeFileReferenceList.fileList.indexOf(fileRef) >= 0)
				{
					WeaveAdminService.messageDisplay(
						"File upload failed",
						"File: " + fileRef.name + "\n" + "Size: " + fileRef.size + "\n" + event.text,
						true
					);
				}
			}
			
			private function handleFileLoadComplete(event:Event):void
			{
				var fileRef:FileReference = event.target as FileReference;
				if (shapeFileReferenceList.fileList.indexOf(fileRef) >= 0)
				{
					var query:DelayedAsyncInvocation = AdminInterface.instance.uploadFile(fileRef);
					query.addAsyncResponder(handleFileUploadComplete, null, fileRef);
				}
			}
			
			private function handleFileUploadComplete(event:ResultEvent, token:Object = null):void
			{
				if (shapeFileReferenceList.fileList.indexOf(token) >= 0)
				{
					fileUploadCount++;
					
					// handle dbf file uploaded
					if (token == _dbfFileToRequestColumnNamesFrom)
					{
						AdminInterface.instance.listDBFFileColumns(_dbfFileToRequestColumnNamesFrom.name);
						
						getDBFDataFromFile(_dbfFileToRequestColumnNamesFrom.name);
						enableAllWidgets();
					}
				}
			}
			
			private function getDBFDataFromFile(file:String):void
			{
				var dbfDataQuery:DelayedAsyncInvocation = AdminInterface.instance.getDBFData(file);
				
				dbfDataQuery.addAsyncResponder(handleDBFDataResult,handleDBFDataFault);
				
				function handleDBFDataResult(event:ResultEvent, token:Object = null):void
				{
					handleKeyColumnsChange();
				}
				function handleDBFDataFault(event:FaultEvent, token:Object = null):void
				{
					
				}
				
			}
				
			
			private var _serverFileBrowser:ServerFileViewer = null;
			private function openServerBrowser():void
			{
				_serverFileBrowser = ServerFileViewer.createServerFileViewer(this, ServerFileViewer.INPUT_SHP);
				_serverFileBrowser.selectButton.addEventListener(MouseEvent.CLICK, serverFileBrowserSelectFile);				
			}
			private function serverFileBrowserSelectFile(e:MouseEvent):void
			{
				var tableName:String = _serverFileBrowser.datagrid.selectedItem.name;
				tableName = tableName.substring(0,tableName.lastIndexOf("."));
				
				shpFileNameInput.text = tableName;
				
				AdminInterface.instance.listDBFFileColumns(tableName+".dbf");
				tableSelector.activeTable = tableName;
				
				getDBFDataFromFile(tableName+".dbf");
				
				PopUpManager.removePopUp(_serverFileBrowser);
				enableAllWidgets();
			}
			
			override public function finish():void
			{
				var alertTitle:String = "Missing parameter";
				if (shpFileNameInput.text == '')
					Alert.show("You must upload a DBF file.", alertTitle);
				else if (dbfKeyColumnsCombo.text == '')
					Alert.show("You must specify the name(s) of the key column(s) in the DBF file.", alertTitle);
				else if (tableSelector.activeTable == '')
					Alert.show("You must specify the name of the destination SQL table.", alertTitle);
				else if (geometryCollectionNameInput.text == '')
					Alert.show("You must specify the name of the GeometryCollection to be visible by users.", alertTitle);
				else
				{
					
					var trimmedKeyCols:String = StringUtil.trimArrayElements(dbfKeyColumnsCombo.text,",");
					var keyCols:Array = trimmedKeyCols.split(","); 
					
					var query:DelayedAsyncInvocation = AdminInterface.instance.convertShapefileToSQLStream(
							shpFileNameInput.text,
							keyCols,
							tableSelector.activeSchema,
							tableSelector.activeTable,
							tableSelector.tableOverwriteCheck.selected,
							geometryCollectionNameInput.text,
							configOverwriteCheck.selected,
							shpKeyTypeCombo.text,
							srsProjectionTxtInput.text,
							nullFlagInput.text,
							dbfImportCheckBox.selected
						);
					
					controlBarEnabled = false;
					query.addAsyncResponder(handleResult, handleFault);
					function handleResult(event:ResultEvent, token:Object = null):void
					{
						controlBarEnabled = true;
					}
					function handleFault(event:FaultEvent, token:Object = null):void
					{
						controlBarEnabled = true;
					}
				}
			}
			
			private function enableAllWidgets(enable:Boolean = true):void
			{
				dbfColumnNames.enabled = enable;
				geoCol.enabled = enable;
				nullFlagInput.enabled = enable;
				keyType.enabled = enable;
				tableSelector.enabled = enable;
//				nextEnabled = enable;
				backEnabled = enable;
//				finishEnabled = enable;
				projectionCode.enabled = enable;
				
				handleKeyColumnsChange();
			}
			
			/**
			 * This function is called to check if the selected key column has unique values.
			 * If the column does not have unique values we set the errorString on the key column
			 * combo box and disable the next and finish buttons.
			 * */
			private function handleKeyColumnsChange():void
			{
				if(dbfKeyColumnsCombo.dataProvider == null ||(dbfKeyColumnsCombo.dataProvider as ArrayCollection).length == 0)
				{
					return;
				}
				
				
				if(!dbfImportCheckBox.selected )
				{
					dbfKeyColumnsCombo.errorString = '';
					finishEnabled = true;
					nextEnabled = true;
					return;
				}
				
//				var selectedKeyColumn:String = dbfKeyColumnsCombo.selectedItem as String;
				
				var selectedKeyColumns:String = dbfKeyColumnsCombo.text;
				
				var selectedKeyColumnsArray:Array = selectedKeyColumns.split(",");
				
				var selectedKeyColumnIndex:Array =[];
				for (var i:int = 0; i<selectedKeyColumnsArray.length; i++)
				{
					selectedKeyColumnIndex.push(AdminInterface.instance.dbfKeyColumns.indexOf(StringUtil.trim(selectedKeyColumnsArray[i])));
					//check if last element added is -1. if True then the keycolumn name is wrong.
					if(selectedKeyColumnIndex[selectedKeyColumnIndex.length-1] == -1)
					{
						dbfKeyColumnsCombo.errorString = "Key Column name '"+ StringUtil.trim(selectedKeyColumnsArray[i]) + "' does not exist";
						finishEnabled = false;
						nextEnabled = false;
						return;
					}
						
				}
				
				var keyIndex:int = AdminInterface.instance.dbfKeyColumns.indexOf(selectedKeyColumns);
				
				var keyColValues:Array = [];
				
				var temp:Array;
				for (var j:int=0; j< AdminInterface.instance.dbfData.length; j++)
				{
					temp = [];
					for (var k:int = 0; k<selectedKeyColumnIndex.length; k++)
					{
						temp.push(AdminInterface.instance.dbfData[j][selectedKeyColumnIndex[k]]);
					}
					keyColValues.push(temp.join(","));
				}
				
				if(!isUnique(keyColValues))
				{
					dbfKeyColumnsCombo.errorString = 'Key values are not unique';
					finishEnabled = false;
					nextEnabled = false;
				}else
				{
					dbfKeyColumnsCombo.errorString = '';
					finishEnabled = true;
					nextEnabled = true;
				}
				
				dbfKeyColumnsCombo.invalidateDisplayList();
			}
			
			private function isUnique(data:Array):Boolean
			{
				var unique:Boolean = true;
				var testUniquenessObject:Object = new Object();
				for(var i:int=0;i<data.length;i++)
				{
					if (testUniquenessObject[data[i]] == undefined)
					{
						testUniquenessObject[data[i]] = true;
						continue;
					}
					unique = false;
					break;
				}
				
				return unique;
			}
			
			
			private function validateProjectionSRS():void
			{
				var alertTitle:String = 'Projection Validation';
				var message:String;
				
				if (WeaveAPI.ProjectionManager.projectionExists(srsProjectionTxtInput.text))
					message = 'Projection code is valid.';
				else
					message = 'Projection code is invalid or not recognized.';
				
				Alert.show(message, alertTitle);
			}
		]]>
	</mx:Script>
</admin:WizardViewStack>
